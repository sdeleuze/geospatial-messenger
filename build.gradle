buildscript {
  ext {
    springBootVersion = '1.3.3.RELEASE'
    kotlinVersion = '1.0.1'
  }
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
  }
}

plugins {
    id "org.asciidoctor.convert" version "1.5.2"
}

apply plugin: 'kotlin'
apply plugin: 'spring-boot'

repositories {
  mavenCentral()
  maven { url 'http://repo.spring.io/milestone' }
  maven { url 'https://dl.bintray.com/kotlin/exposed' } // exposed
  maven { url 'https://dl.bintray.com/sdeleuze/maven/' } // postgis-geojson
}

jar {
  baseName = 'geospatial-messenger'
  version = '1.0.0-SNAPSHOT'
  dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}

ext['snippetsDir'] = file('build/generated-snippets')
ext['spring.version'] = '4.3.0.RC1'
ext['jackson.version'] = '2.7.3'

configurations {
  all*.exclude group: 'postgresql', module: 'postgresql'
}

dependencies {
  compile('org.springframework.boot:spring-boot-starter-web') {
    exclude module: 'spring-boot-starter-validation'
  }
  compile('org.springframework.boot:spring-boot-starter-jdbc')
  compile('org.springframework.boot:spring-boot-devtools')


  compile("org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}")
  compile("org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}")
  compile("com.fasterxml.jackson.module:jackson-module-kotlin:${project.ext['jackson.version']}")

  compile('org.jetbrains.exposed:exposed:0.4.2') {
    exclude module: 'h2'
    exclude module: 'joda-time'
  }
  compile('org.postgresql:postgresql:9.4.1208')
  compile('net.postgis:postgis-jdbc:2.2.0')
  compile('com.github.mayconbordin:postgis-geojson:1.1') // https://github.com/sdeleuze/postgis-geojson

  testCompile('org.springframework.boot:spring-boot-starter-test')
  testCompile('org.springframework.restdocs:spring-restdocs-mockmvc:1.0.1.RELEASE')
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.12'
}


test {
    outputs.dir snippetsDir
}

asciidoctor {
    attributes 'snippets': snippetsDir
    inputs.dir snippetsDir
    outputDir "build/asciidoc"
    dependsOn test
    sourceDir 'src/main/asciidoc'
}
